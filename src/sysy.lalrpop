use crate::ast::*;

grammar;

// 约束 lexer 的行为
match {
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
  _
}

// CompUnit ::= FuncDef;
pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { func_def };

// Decl ::= ConstDecl | VarDecl;
Decl: Decl = {
  <const_decl: ConstDecl> => Decl::Const(const_decl),
  <var_decl: VarDecl> => Decl::Var(var_decl),
};

// ConstDecl ::= "const" BType ConstDef {"," ConstDef} ";";
ConstDecl: ConstDecl = {
  "const" <btype: BType> <const_def: ConstDef> <cds: ("," <ConstDef>)*> ";" => {
    let mut const_defs = vec![const_def];
    const_defs.extend(cds);
    ConstDecl { btype, const_defs }
  }
};

// BType ::= "int";
BType: BType = {
  "int" => BType::Int,
};

// ConstDef ::= Ident "=" ConstInitVal;
ConstDef: ConstDef = {
  <id: Ident> "=" <init_val: ConstInitVal> => ConstDef { ident: id, init_val },
};

// ConstInitVal ::= ConstExp;
ConstInitVal: ConstInitVal = {
  <exp: ConstExp> => ConstInitVal::ConstExp(exp),
};

// VarDecl ::= BType VarDef {"," VarDef} ";";
VarDecl: VarDecl = {
  <btype: BType> <var_def: VarDef> <vds: ("," <VarDef>)*> ";" => {
    let mut defs = vec![var_def];
    defs.extend(vds);
    VarDecl { btype, defs }
  }
};

// VarDef ::= IDENT | IDENT "=" InitVal;
VarDef: VarDef = {
  <id: Ident> => VarDef { ident: id, init_val: None },
  <id: Ident> "=" <init_val: InitVal> => VarDef { ident: id, init_val: Some(init_val) },
};

// InitVal ::= Exp;
InitVal: InitVal = {
  <exp: Exp> => InitVal::Exp(exp),
};

// FuncDef ::= FuncType Ident "(" ")" Block
FuncDef: FuncDef = {
  <ret_ty: FuncType> <id: Ident> "(" ")" <block: Block> => {
    FuncDef { ret_ty, ident: id, block }
  }
}

// FuncType ::= "int";
FuncType: FuncType = "int" => FuncType::Int;

// Block ::= "{" {BlockItem} "}";
Block: Block = {
  "{" <items: (BlockItem)*> "}" => Block { items },
}

// BlockItem ::= Decl | Stmt;
BlockItem: BlockItem = {
  <decl: Decl> => BlockItem::Decl(decl),
  <stmt: Stmt> => BlockItem::Stmt(stmt),
};

// Stmt ::= LVal "=" Exp ";" | "return" Exp ";";
Stmt: Stmt = {
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign(lval, exp),
  "return" <exp: Exp> ";" => Stmt::Return(exp),
};

// Exp ::= LOrExp;
Exp: Exp = {
  <exp: LOrExp> => Exp::LOr(exp),
};

// LVal ::= IDENT;
LVal: LVal = {
  <id: Ident> => LVal::Ident(id),
};

// PrimaryExp ::= "(" Exp ")" | LVal | Number;
PrimaryExp: PrimaryExp = {
  <num: Number> => PrimaryExp::Number(num),
  <lval: LVal> => PrimaryExp::LVal(lval),
  "(" <exp: Exp> ")" => PrimaryExp::Paren(Box::new(exp)),
};

// UnaryExp ::= PrimaryExp | UnaryOp UnaryExp
UnaryExp: UnaryExp = {
  <exp: PrimaryExp> => UnaryExp::PrimaryExp(exp),
  <op: UnaryOp> <exp: UnaryExp> => UnaryExp::Unary(
    op,
    Box::new(exp),
  ),
};

// MulExp ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
MulExp: MulExp = {
  <exp: UnaryExp> => MulExp::Unary(exp),
  <l: MulExp> <op: MulOp> <r: UnaryExp> => MulExp::MulUnary(
    Box::new(l),
    op,
    r,
  ),
};

// AddExp ::= MulExp | AddExp ("+" | "-") MulExp;
AddExp: AddExp = {
  <exp: MulExp> => AddExp::Mul(exp),
  <l: AddExp> <op: AddOp> <r: MulExp> => AddExp::AddMul(
    Box::new(l),
    op,
    r,
  ),
};

// RelExp ::= AddExp | RelExp ("<" | "<=" | ">" | ">=") AddExp;
RelExp: RelExp = {
  <exp: AddExp> => RelExp::Add(exp),
  <l: RelExp> <op: RelOp> <r: AddExp> => RelExp::RelAdd(
    Box::new(l),
    op,
    r,
  ),
};

// EqExp ::= RelExp | EqExp ("==" | "!=") RelExp;
EqExp: EqExp = {
  <exp: RelExp> => EqExp::Rel(exp),
  <l: EqExp> <op: EqOp> <r: RelExp> => EqExp::EqRel(
    Box::new(l),
    op,
    r
  )
};

// LAndExp ::= EqExp | LAndExp "&&" EqExp;
LAndExp: LAndExp = {
  <eq: EqExp> => LAndExp::Eq(eq),
  <l: LAndExp> "&&" <r: EqExp> => LAndExp::LAndEq(
    Box::new(l),
    r,
  ),
};

// LOrExp ::= LAndExp | LOrExp "||" LAndExp;
LOrExp: LOrExp = {
  <land: LAndExp> => LOrExp::LAnd(land),
  <l: LOrExp> "||" <r: LAndExp> => LOrExp::LOrLAnd(
    Box::new(l),
    r,
  ),
};

// ConstExp ::= Exp;
ConstExp: ConstExp = {
  <exp: Exp> => ConstExp { exp },
}


UnaryOp: UnaryOp = {
  "+" => UnaryOp::Plus,
  "-" => UnaryOp::Minus,
  "!" => UnaryOp::Not,
};

MulOp: MulOp = {
  "*" => MulOp::Mul,
  "/" => MulOp::Div,
  "%" => MulOp::Mod,
};

AddOp: AddOp = {
  "+" => AddOp::Add,
  "-" => AddOp::Sub,
};

RelOp: RelOp = {
  "<" => RelOp::Lt,
  "<=" => RelOp::Le,
  ">" => RelOp::Gt,
  ">=" => RelOp::Ge,
};

EqOp: EqOp = {
  "==" => EqOp::Eq,
  "!=" => EqOp::Ne,
};


Number: i32 = <num: IntConst> => <>;

Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
};
