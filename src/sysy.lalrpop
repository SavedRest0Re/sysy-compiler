use crate::ast::*;

grammar;

// 约束 lexer 的行为
match {
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
  _
}

// CompUnit ::= [CompUnit] (Decl | FuncDef)
pub CompUnit: CompUnit = <items: (GlobalItem)+> => CompUnit { items };


// 内联展开 解决 shift/reduce 冲突
// "int" VarDecl ";"
// "int" Ident "(" ")" Block
GlobalItem: GlobalItem = {
  // <func_def: FuncDef> => GlobalItem::FuncDef(func_def),
  // <global_decl: Decl> => GlobalItem::GlobalDecl(global_decl),

  <c: ConstDecl> => GlobalItem::GlobalDecl(Decl::Const(c)),

  "void" <ident:Ident> "(" <params:FuncFParams?> ")" <block:Block> => {
    GlobalItem::FuncDef(FuncDef {
      ret_ty: FuncType::Void,
      ident,
      params,
      block,
    })
  },

  "int" <ident: Ident> "(" <params:FuncFParams?> ")" <block:Block> => {
    GlobalItem::FuncDef(FuncDef {
      ret_ty: FuncType::Int,
      ident,
      params,
      block,
    })
  },

  "int" <var_def: VarDef> <rest: ("," <VarDef>)*> ";" => {
    let mut defs = vec![var_def];
    defs.extend(rest);

    GlobalItem::GlobalDecl(Decl::Var(VarDecl {
      btype: BType::Int,
      defs,
    }))
  }

}

// Decl ::= ConstDecl | VarDecl;
Decl: Decl = {
  <const_decl: ConstDecl> => Decl::Const(const_decl),
  <var_decl: VarDecl> => Decl::Var(var_decl),
};

// ConstDecl ::= "const" BType ConstDef {"," ConstDef} ";";
ConstDecl: ConstDecl = {
  "const" <btype: BType> <const_def: ConstDef> <cds: ("," <ConstDef>)*> ";" => {
    let mut const_defs = vec![const_def];
    const_defs.extend(cds);
    ConstDecl { btype, const_defs }
  }
};

// BType ::= "int";
BType: BType = {
  "int" => BType::Int,
};

// ConstDef ::= IDENT {"[" ConstExp "]"} "=" ConstInitVal;
ConstDef: ConstDef = {
  <ident: Ident> <dims: ("[" <ConstExp> "]")*> "=" <init_val: ConstInitVal> => ConstDef { ident, init_val, dims },
};

// ConstInitVal ::= ConstExp | "{" [ConstInitVal {"," ConstInitVal}] "}";
// { 0, {1}, 2, {3, {4}} }
ConstInitVal: ConstInitVal = {
  <exp: ConstExp> => ConstInitVal::ConstExp(exp),
  "{" "}" => ConstInitVal::ConstInitList(vec![]),
  "{" <h: ConstInitVal> <t: ("," <ConstInitVal>)*> "}" => {
    let mut const_init_list = vec![h];
    const_init_list.extend(t);
    ConstInitVal::ConstInitList(const_init_list)
  }
};

// VarDecl ::= BType VarDef {"," VarDef} ";";
VarDecl: VarDecl = {
  <btype: BType> <var_def: VarDef> <vds: ("," <VarDef>)*> ";" => {
    let mut defs = vec![var_def];
    defs.extend(vds);
    VarDecl { btype, defs }
  }
};

// VarDef ::= IDENT {"[" ConstExp "]"} | IDENT {"[" ConstExp "]"} "=" InitVal;
VarDef: VarDef = {
  <ident: Ident> <dims: ("[" <ConstExp> "]")*> => VarDef { ident, init_val: None, dims },
  <ident: Ident> <dims: ("[" <ConstExp> "]")*> "=" <init_val: InitVal> => VarDef { ident, init_val: Some(init_val), dims },
};

// InitVal ::= Exp | "{" [InitVal {"," InitVal}] "}";
InitVal: InitVal = {
  <exp: Exp> => InitVal::Exp(exp),
  "{" "}" => InitVal::InitList(vec![]),
  "{" <h: InitVal> <t: ("," <InitVal>)*> "}" => {
    let mut init_list = vec![h];
    init_list.extend(t);
    InitVal::InitList(init_list)
  }
};

// FuncDef ::= FuncType Ident "(" ")" Block
FuncDef: FuncDef = {
  <ret_ty: FuncType> <ident: Ident> "(" <params: FuncFParams?> ")" <block: Block> => {
    FuncDef { ret_ty, ident, params, block }
  }
};

// FuncType ::= "int";
FuncType: FuncType = {
  "int" => FuncType::Int,
  "void" => FuncType::Void,
};

// FuncFParams ::= FuncFParam {"," FuncFParam};
FuncFParams: FuncFParams = {
  <h: FuncFParam> <t: ("," <FuncFParam>)*> => {
    let mut params = vec![h];
    params.extend(t);
    FuncFParams { params }
  }
};

// FuncFParam ::= BType IDENT ["[" "]" {"[" ConstExp "]"}];
FuncFParam: FuncFParam = {
  <btype: BType> <ident: Ident> => FuncFParam { btype, ident, dims: vec![] },
  <btype: BType> <ident: Ident> "[" "]" <dims: ("[" <ConstExp> "]")*> => {
    let mut ds = vec![None];
    for d in dims {
      ds.push(Some(d));
    }
    FuncFParam { btype, ident, dims: ds }
  }
};

// Block ::= "{" {BlockItem} "}";
Block: Block = {
  "{" <items: (BlockItem)*> "}" => Block { items },
}

// BlockItem ::= Decl | Stmt;
BlockItem: BlockItem = {
  <decl: Decl> => BlockItem::Decl(decl),
  <stmt: Stmt> => BlockItem::Stmt(stmt),
};

// Stmt ::= LVal "=" Exp ";" | [Exp] ";" | Block | "if" "(" Exp ")" Stmt ["else" Stmt] | "return" Exp ";"
//        | "while" "(" Exp ")" Stmt | "break" ";" | "continue" ";" ;

Stmt: Stmt = {
  <matched: MatchedStmt> => matched,
  <open: OpenStmt> => open,
};

MatchedStmt: Stmt = {
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign(lval, exp),
  <exp: Exp?> ";" => Stmt::Exp(exp),
  <block: Block> => Stmt::Block(block),

  "if" "(" <exp: Exp> ")" <then: MatchedStmt> "else" <else_stmt: MatchedStmt> => Stmt::If(exp, Box::new(then), Some(Box::new(else_stmt))),

  "return" <exp: Exp?> ";" => Stmt::Return(exp),

  "while" "(" <cond: Exp> ")" <body: MatchedStmt> => Stmt::While(cond, Box::new(body)),

  "break" ";" => Stmt::Break,
  "continue" ";" => Stmt::Continue,
};

OpenStmt: Stmt = {
  "if" "(" <cond: Exp> ")" <then: Stmt> => {
    Stmt::If(cond, Box::new(then), None)
  },

  "if" "(" <cond: Exp> ")" <then: MatchedStmt> "else" <else_stmt: OpenStmt> => {
    Stmt::If(cond, Box::new(then), Some(Box::new(else_stmt)))
  },

  "while" "(" <cond: Exp> ")" <body: OpenStmt> => Stmt::While(cond, Box::new(body)),
}

// Exp ::= LOrExp;
Exp: Exp = {
  <exp: LOrExp> => Exp::LOr(exp),
};

// LVal ::= IDENT {"[" Exp "]"};;
LVal: LVal = {
  <id: Ident> <dims: ("[" <Exp> "]")*> => {
    LVal::GLVal(id, dims)
  }
};

// PrimaryExp ::= "(" Exp ")" | LVal | Number;
PrimaryExp: PrimaryExp = {
  <num: Number> => PrimaryExp::Number(num),
  <lval: LVal> => PrimaryExp::LVal(lval),
  "(" <exp: Exp> ")" => PrimaryExp::Paren(Box::new(exp)),
};

// UnaryExp ::= PrimaryExp | UnaryOp UnaryExp | IDENT "(" [FuncRParams] ")"
UnaryExp: UnaryExp = {
  <exp: PrimaryExp> => UnaryExp::PrimaryExp(exp),
  <op: UnaryOp> <exp: UnaryExp> => UnaryExp::Unary(
    op,
    Box::new(exp),
  ),
  <ident: Ident> "(" <params: FuncRParams?> ")" => UnaryExp::FuncCall(ident, params),
};

// FuncRParams ::= Exp {"," Exp};
FuncRParams: FuncRParams = {
  <h: Exp> <t: ("," <Exp>)*> => {
    let mut args = vec![h];
    args.extend(t);
    FuncRParams { args }
  }
};

// MulExp ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
MulExp: MulExp = {
  <exp: UnaryExp> => MulExp::Unary(exp),
  <l: MulExp> <op: MulOp> <r: UnaryExp> => MulExp::MulUnary(
    Box::new(l),
    op,
    r,
  ),
};

// AddExp ::= MulExp | AddExp ("+" | "-") MulExp;
AddExp: AddExp = {
  <exp: MulExp> => AddExp::Mul(exp),
  <l: AddExp> <op: AddOp> <r: MulExp> => AddExp::AddMul(
    Box::new(l),
    op,
    r,
  ),
};

// RelExp ::= AddExp | RelExp ("<" | "<=" | ">" | ">=") AddExp;
RelExp: RelExp = {
  <exp: AddExp> => RelExp::Add(exp),
  <l: RelExp> <op: RelOp> <r: AddExp> => RelExp::RelAdd(
    Box::new(l),
    op,
    r,
  ),
};

// EqExp ::= RelExp | EqExp ("==" | "!=") RelExp;
EqExp: EqExp = {
  <exp: RelExp> => EqExp::Rel(exp),
  <l: EqExp> <op: EqOp> <r: RelExp> => EqExp::EqRel(
    Box::new(l),
    op,
    r
  )
};

// LAndExp ::= EqExp | LAndExp "&&" EqExp;
LAndExp: LAndExp = {
  <eq: EqExp> => LAndExp::Eq(eq),
  <l: LAndExp> "&&" <r: EqExp> => LAndExp::LAndEq(
    Box::new(l),
    r,
  ),
};

// LOrExp ::= LAndExp | LOrExp "||" LAndExp;
LOrExp: LOrExp = {
  <land: LAndExp> => LOrExp::LAnd(land),
  <l: LOrExp> "||" <r: LAndExp> => LOrExp::LOrLAnd(
    Box::new(l),
    r,
  ),
};

// ConstExp ::= Exp;
ConstExp: ConstExp = {
  <exp: Exp> => ConstExp { exp },
}


UnaryOp: UnaryOp = {
  "+" => UnaryOp::Plus,
  "-" => UnaryOp::Minus,
  "!" => UnaryOp::Not,
};

MulOp: MulOp = {
  "*" => MulOp::Mul,
  "/" => MulOp::Div,
  "%" => MulOp::Mod,
};

AddOp: AddOp = {
  "+" => AddOp::Add,
  "-" => AddOp::Sub,
};

RelOp: RelOp = {
  "<" => RelOp::Lt,
  "<=" => RelOp::Le,
  ">" => RelOp::Gt,
  ">=" => RelOp::Ge,
};

EqOp: EqOp = {
  "==" => EqOp::Eq,
  "!=" => EqOp::Ne,
};


Number: i32 = <num: IntConst> => <>;

Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
};
